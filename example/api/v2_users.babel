namespace users

include v2_common

struct QuotaInfo:
    doc::
        The space quota info for a user.

    quota UInt64::
        The user's total quota allocation (bytes).
    normal UInt64::
        The user's used quota outside of shared folders (bytes).
    shared UInt64::
        The user's used quota in shared folders (bytes).

    optional:
        datastores UInt64 nullable::
            The user's used quota in datastores (bytes).

    example default:
        quota=1000000
        normal=1000
        shared=500
        datastores=42

struct Team:
    doc::
        Information relevant to a team.

    name String::
        The name of the team.

    example default:
        name="Acme, Inc."

struct AccountInfo:
    doc::
        Information for a user's account.

    display_name String::
        The full name of a user.
    account_id String::
        The user's unique Dropbox ID.
    email String::
        The user's e-mail address.
    country String(min_length=2, max_length=2)::
        The user's two-letter country code, if available.
    referral_link String::
        The user's :link:`referral link https://www.dropbox.com/referrals`.
    quota QuotaInfo::
        The user's quota.
    is_paired Boolean::
        Whether the user has a personal and business account.
    team Team nullable::
        If this account is a member of a team.

    example default:
        display_name="Jon Snow"
        account_id="314159"
        email="jsnow@westeros.com"
        country="US"
        referral_link="https://db.tt/ZITNuhtI"
        is_paired=true

    example unpaired:
        display_name="Jon Snow"
        account_id="314159"
        email="jsnow@westeros.com"
        country="US"
        referral_link="https://db.tt/ZITNuhtI"
        is_paired=false
        team=null

struct AccountInfoRequest:
    doc::
        Stub.

    account_id String::
        A user's account identifier. Use "me" to get information for the
        current account.

op Info:
    doc::
        Get information about a user's account.

    request:
        info AccountInfoRequest

    response:
        info AccountInfo
