namespace files

# Python doesn't support %z so we hardcode +0000 here.
# TODO(kelkabany): Handle this in the Python code generator
alias DbxTimestamp = Timestamp(format="%a, %d %b %Y %H:%M:%S +0000")

struct PathRef
    path String
        "Path from root. Should be an empty string for root."

struct FileRef extends PathRef
    rev String?
        "Revision of target file."

struct FileInfo
    name String
        "Name of file."

struct SubError
    reason String
        "A code indicating the type of error."

union DownloadError
    disallowed SubError
    no_file SubError
    unknown*

route download (FileRef, FileInfo, DownloadError)
    "Download a file in a user's Dropbox."

    attrs
        host="content"
        style="download"

struct UploadSessionStart
    upload_id String
        "A unique identifier for the upload session."

route upload/start (Empty, UploadSessionStart, Empty)
    "Start an upload session."

    attrs
        host="content"
        style="upload"

struct UploadAppend
    upload_id String
        "Identifies the upload session to append data to."
    offset UInt64
        "The offset into the file of the current chunk of data being uploaded.
        It can also be thought of as the amount of data that has been uploaded
        so far. We use the offset as a sanity check."

struct IncorrectOffsetError
    correct_offset UInt64

union UploadAppendError
    not_found
        ":field:`upload_id` was not found."
    closed
        "Upload session was closed."
    incorrect_offset IncorrectOffsetError

route upload/append (UploadAppend, Empty, Empty)
    "Start an upload session."

    attrs
        host="content"
        style="upload"

struct UpdateParentRev
    parent_rev String

union UploadMode
    "The action to take when a file path conflict exists."

    add
        "If the path is already occupied, the upload is rejected. You can call
        the :route:`upload` endpoint again and try a different path."
    overwrite
        "On a conflict, the target is overridden."
    update UpdateParentRev
        "On a conflict, only overwrite the target if the parent_rev matches."

struct UploadCommit
    path String
        "Path in the user's Dropbox to save the file."
    mode UploadMode
        "The course of action to take if a file already exists at :field:`path`."
    append_to UploadAppend?
        "If specified, the current chunk of data should be appended to an
        existing upload session."
    autorename Boolean? = false
        "Whether the file should be autorenamed in the event of a conflict."
    client_modified_utc UInt64? = null
        "Self reported time of when this file was created or modified."
    mute Boolean? = false
        "Whether the devices that the user has linked should notify them of the
        new or updated file."

union ConflictReason
    folder
        "Conflict with a folder."
    file
        "Conflict with a file."
    autorename_failed
        "Could not autorename."

struct ConflictError
    reason ConflictReason

union UploadCommitError
    conflict ConflictError
    no_write_permission
        "User does not have permission to write in the folder. An example of
        this is if the folder is a read-only shared folder."
    insufficient_quota
        "User does not have sufficient space quota to save the file."

route upload (UploadCommit, FileInfo, UploadCommitError)
    "Use this endpoint to either finish an ongoing upload session that was
    begun with :route:`upload/start` or upload a file in one shot."

    attrs
        host="content"
        style="upload"

struct File
    "A file resource"

    client_modified DbxTimestamp
        "For files, this is the modification time set by the desktop client
        when the file was added to Dropbox. Since this time is not verified
        (the Dropbox server stores whatever the desktop client sends up), this
        should only be used for display purposes (such as sorting) and not,
        for example, to determine if a file has changed or not."
    server_modified DbxTimestamp
        "The last time the file was modified on Dropbox."
    rev String
        "A unique identifier for the current revision of a file. This field is
        the same rev as elsewhere in the API and can be used to detect changes
        and avoid conflicts."
    size UInt64
        "The file size in bytes."

struct Folder
    "A folder resource"

union Metadata
    file File
    folder Folder

struct Entry
    metadata Metadata
    name String
        "The name of the resource as seen by the user in their Dropbox."

union GetMetadataError
    not_found
        "File was not found at the specified path."

route get_metadata (FileRef, Entry, GetMetadataError)
    "Returns the metadata for a file or folder."

struct FolderContentsBase
    cursor String
        "Pass the cursor into :route:`ListFolderContinue` to see what's changed
        in the folder since your previous query."
    has_more Boolean
        "If true, then there are more entries available."

struct FolderContentsInit extends FolderContentsBase
    entries List(data_type=Entry)
        "Each entry is a resource in the folder."

union ListFolderError
    not_found
        "File was not found at the specified path."
    not_folder
        "Entry at path was not a folder."

    # reset can only ever happen on ListFolderContinue. This demonstrates an
    # issue with Babel-generated unions where ListFolderContinue has a superset
    # of errors of ListFolderInit. It's ideal to share code between the two,
    # but one requires ListFolderError to be returned, whereas the other one
    # would hypothetically require ListFolderContinueError. If both use the
    # same error data type, then ListFolderInit will specify errors that do not
    # actually occur, which is what "reset" is in this case.
    reset
        "Placeholder. This isn't used yet."

route list_folder (PathRef, FolderContentsInit, ListFolderError)
    "Returns the contents of a folder."

struct CursorRef
    cursor String

union ContinueType
    continue List(data_type=Entry)
    restart List(data_type=Entry)

struct FolderContentsContinue extends FolderContentsBase
    entries ContinueType

route list_folder/continue (CursorRef, FolderContentsContinue, ListFolderError)
    "Once a cursor has been retrieved from :route:`ListFolder`, use this to
    paginate through all files and retrieve updates to the folder."
